/**
 * Tests for Wollok numbers => basic arithmetic operations
 */
describe "arithmetic operations"{

	test "Addition" {
		assert.equals(2, 1 + 1)
		assert.equals(2.5, 1 + 1.5)
		assert.equals(2.5, 1.5 + 1)
		assert.equals(3.0, 1.3 + 1.7)
		assert.equals(4, 4 + 0.0)
		assert.equals(4, 4.0 + 0.0)
		assert.equals(4, 4.0 + 0)
		assert.equals(4, 4 + 0)
	}

	test "Add with failing parameters" {
		assert.throwsException( { 3 + new Cuenta() })
		assert.throwsException( { 3 + "pepe" })
	}

	test "Add 1 to null" {
		assert.throwsException({ 1 + null })
	}

	test "Add several decimals" {
		assert.equals(4.00001, 3.000004 + 1.000006)
		assert.equals(4.00001, 3.000002 + 1.000006)
		assert.equals(4, 3.000002 + 1.000002)
	}

	test "Very big integer add" {
		const a = 100000000000000000
		assert.equals(100000000000000001, a + 1)
	}

	test "Subtraction"{
		assert.equals(0, 1 - 1)
		assert.equals(-0.5, 1 - 1.5)
		assert.equals(0.5, 1.5 - 1)
		assert.equals(0.0, 1.5 - 1.5)
	}

	test "Subtract using null" {
		assert.throwsException({ 1 - null })
	}

	test "Subtract fail" {
		assert.throwsException({ 4 - "a" })
	}

	test "Subtract several decimals" {
		assert.equals(-0.00001, 4.000004 - 4.000006)
		assert.equals(0, 4.000007 - 4.000006)
		assert.equals(2, 3.000002 - 1.000001)
		assert.equals(1.99999, 3.000002 - 1.000006)
		assert.equals(1.99978, 3.000002 - 1.000222)
	}

	test "Product"{
		assert.equals(1, 1 * 1)
		assert.equals(1.5, 1 * 1.5)
		assert.equals(1.5, 1.5 * 1)
		assert.equals(2.25, 1.5 * 1.5)
		assert.equals(1.96, 1.4 * 1.4)
	}

	test "Multiply using null" {
		assert.throwsException({ 1 * null })
	}

	test "Multiply fail" {
		assert.throwsException({ 4 * "a" })
	}

	test "Multiply several decimals" {
		assert.equals(8, 4.00000000001 * 2.000000000003)
		assert.equals(0, 4.00222222222222000000001 * 0)
		assert.equals(4.00270, 4.00222222222222000000001 * 1.00012)
		assert.equals(4.00415, 4.00222522222222000000001 * 1.00048)
	}

	test "Multiply by zero" {
		assert.equals(6.2500000 * 0.5 * 0, 0)
		assert.equals(6.2500000 * 0, 0)
		assert.equals(0.0 * 0, 0)
		assert.equals(0.0 * 0.0, 0)
		assert.equals(0 * 0.0, 0)
	}

	test "Very big integer multiply" {
		const a = 100000000000000000
		assert.equals(100000000000000000, a * 1)
	}

	test "Division" {
		assert.equals(1.5, 3 / 2)
		assert.equals(1.5, 3 / 2.0)
		assert.equals(1.5, 3.0 / 2)
		assert.equals(1.5, 2.25 / 1.5)
		assert.equals(0.3, 3 / 10)
		assert.equals(2.5, 5 / 2)
		assert.equals(2, 4 / 2)
		assert.equals(2, 4 / 2.0)
	}

	test "Divide zero" {
		assert.equals(0, 0 / 10)
		assert.equals(0, 0 / 10.0)
		assert.equals(0, 0.0 / 10.0)
		assert.equals(0, 0 / 10.0)
		assert.equals(0, 0 / 100.0)
		assert.equals(0, 1 / 100000000000.0)
	}

	test "Divide several decimals" {
		assert.equals(0.51235, 5.123456 / 10.00000011)
		assert.equals(0.51235, 5.123456 / 10)
		assert.equals(0.51235, 5123456 / 10000000)
	}

	test "Division by zero" {
		assert.throwsException({ 1 / 0 })
		assert.throwsException({ 1.0 / 0 })
		assert.throwsException({ 1.0 / 0.0 })
		assert.throwsException({ 1 / 0.0 })
	}

	test "Divide periodic decimals" {
		assert.equals(0.72727, 40 / 55)
		assert.equals(0.72727, 40 / 55.0)
		assert.equals(0.72727, 40.0 / 55.0)
		assert.equals(0.72727, 40.0 / 55)
		assert.equals(0.33333, 1 / 3)
		assert.equals(0.66667, 2 / 3)
	}

	test "Divide decimals" {
		assert.equals(0.3, 3 / 10.0)
		assert.equals(0.3, 3.0 / 10.0)
		assert.equals(2.5, 5 / 2.0)
		assert.equals(2, 4.0 / 2.0)
	}

	test "Very big integer divide" {
		const a = 100000000000000000
		assert.equals(100000, a / 1000000000000)
	}

	test "Division using null" {
		assert.throwsException( { 3 / null })
	}

	test "Division fail" {
		assert.throwsException( { 3 / "a" })
	}

	test "Decimal Exponentiation"{
		assert.equals(3.0, 9 ** 0.5)
		assert.equals(1.5, 2.25 ** 0.5)
	}

	test "Negative Exponentiation" {
		assert.equals(0.2, 5 ** (-1))
		assert.equals(0.2, 5 ** (-1.0))
		assert.equals(0.2, 5.0 ** (-1.0))
		assert.equals(0.2, 5.0 ** (-1))
		assert.equals(1, 5.0 ** 0)
		assert.equals(1, 5.0 ** 0.0)
		assert.equals(1, 5 ** 0)
		assert.equals(1, 5 ** 0.0)
	}

	test "Integer Exponentiation" {
		assert.equals(25, 5 ** 2)
		assert.equals(25, 5 ** 2.0)
		assert.equals(25.0, 5.0 ** 2)
		assert.equals(25.0, 5.0 ** 2.0)
	}

	test "Exponentiation using null" {
		assert.throwsException({ 4 ** null } )
	}

	test "Exponentiation Fail" {
		assert.throwsException({ 4 ** "a" } )
	}

	test "Exponentiation precedence" {
		assert.equals(24, 3 * 2 ** 3)
		assert.equals(36, 4.0 * 3 ** 2)
		assert.equals(5, 1 + 2 ** 2)
	}

	test "Module"{
		assert.equals(1, 10 % 3)
	}

}

describe "mixed number types operations" {

	test "test equals" {
		assert.equals(2 * 2.0, 2.0 * 2)
		assert.equals(2 * 2.0, 2.0 * 2)

		assert.equals(1 + 2.0, 1.0 + 2)
		assert.equals(1 + 2.0, 1.0 + 2)

		assert.equals(1 - 2.0, 1.0 - 2)
		assert.equals(1 - 2.0, 1.0 - 2)

		assert.equals(1 / 2.0, 1.0 / 2)
		assert.equals(1 / 2.0, 1.0 / 2)
	}

	test "test float operations" {
		assert.equals(100 * 1.1, 110.0)
		assert.equals(100 * 1.1, 110.0)

		assert.equals(1.4 * 1.4, 1.96)
		assert.equals(1.4 * 1.4, 1.96)
	}

}
